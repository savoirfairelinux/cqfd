# Maintainer: Jérôme Oufella <jerome.oufella@savoirfairelinux.com>

pkgname=cqfd-git
pkgver=master
pkgrel=1
pkgdesc='Wrap commands in controlled Docker containers using docker.'
arch=(any)
url="https://github.com/savoirfairelinux/$pkgname"
license=(GPL-3.0-only)
depends=(bash)
makedepends=(asciidoctor bash-completion git)
checkdepends=(shellcheck)
source=("$pkgname::git+https://github.com/savoirfairelinux/${pkgname%-git}.git")
sha256sums=(SKIP)

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$pkgname"
	make doc
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	depends+=(docker)
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir" PREFIX="/usr" install
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
